module core.draw

pub category Plane {
    fun get(&self, x: Int, y: Int) -> Color
}

pub category SizedPlane: Plane {
    fun width(&self) -> Int
    fun height(&self) -> Int
}

pub category MutablePlane: Plane {
    fun set(&mut self, x: Int, y: Int, color: Color)
}

pub category PrimitivePlane: Plane {
    init(flattening plane: &SizedPlane) -> Self
}

extend Plane {
    pub fun flatten<T>(&self) -> T where T: PrimitivePlane {
        T(flattening: self)
    }
}

pub struct PlaneSlice<Inner> where Inner: Plane {
    pub inner: Inner
    pub x: Int
    pub y: Int
    width: Int
    height: Int
}

extend PlaneSlice: Plane {
    pub fun get(&self, x: Int, y: Int) -> Color {
        self.inner.get(x: x + self.x, y: y + self.y)
    }
}

extend PlaneSlice: SizedPlane where Inner: SizedPlane {
    pub fun width(&self) -> Int {
        self.width
    }

    pub fun height(&self) -> Int {
        self.height
    }
}

extend PlaneSlice: MutablePlane where Inner: MutablePlane {
    pub fun set(mut &self, x: Int, y: Int, color: Color) {
        self.inner.set(x: x + self.x, y: y + self.y, color: color)
    }
}
